plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

description = 'Custom Hibernate3 dialects for the uPortal ecosystem, by Apereo'

sourceCompatibility = 1.8

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
    jcenter()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:28.0-jre'
    implementation "org.hibernate:hibernate-core:${hibernateVersion}"

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

/* Release Management
 *
 * Based on Sonatype's guide for publishing w/ Gradle:  http://central.sonatype.org/pages/gradle.html
 */
ext {
    publishingUsername = project.hasProperty('ossrhUsername') ? project.getProperty('ossrhUsername') : ""
    publishingPassword = project.hasProperty('ossrhPassword') ? project.getProperty('ossrhPassword') : ""
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}
tasks.withType(Javadoc) {
    // FIXME: Fix errors rather than supressing them
    failOnError = false
}
tasks.withType(Jar) {
    from(project.projectDir) {
        include "${rootDir}/LICENSE"
        into 'META-INF'
    }
}
artifacts {
    archives javadocJar, sourcesJar
}
signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: publishingUsername, password: publishingPassword)
            }

            pom.project {
                name 'uPortal-hibernate3-support'
                description 'Custom Hibernate3 dialects for the uPortal ecosystem, by Apereo'
                url 'https://github.com/bjagg/uPortal-hibernate3-support'

                scm {
                    connection 'scm:git:git://github.com/bjagg/uPortal-hibernate3-support.git'
                    url 'https://github.com/bjagg/uPortal-hibernate3-support'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        organization 'FBMS Developers'
                        organizationUrl 'https://github.com/bjagg/uPortal-hibernate3-support/graphs/contributors'
                    }
                }
            }
        }
    }
}
release {
    tagTemplate = 'v${version}' // Looks like it should be a GString, but not necessary
    git {
        requireBranch = ''
    }
}
afterReleaseBuild.dependsOn uploadArchives